plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
    id 'jacoco'
    id "me.champeau.jmh" version "0.6.5"
    id "net.researchgate.release" version "2.8.1"
//    id "com.github.spotbugs" version "4.7.3"
    id "com.github.hierynomus.license" version "0.16.1"
}

group = "io.rapidw.mqtt"
description = "Rapidw MQTT codec"

ext {
    url = "https://github.com/rapidw/mqtt-codec"
    vcsUrl = "${url}.git"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.slf4j:slf4j-api:1.7.30"
    api "io.netty:netty-codec:4.1.45.Final"

    testImplementation "ch.qos.logback:logback-classic:1.2.3"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.6.0"
    testImplementation "org.assertj:assertj-core:3.11.1"

    jmh "io.netty:netty-codec-mqtt:4.1.45.Final"
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

license {
    header project.file("gradle/license-header")
}

javadoc {
    options {
        encoding = "UTF-8"
        locale "en_US"
        jFlags "-Duser.language=en_US"
    }

    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption("html5", true)
    }
}

tasks.withType(Test) {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = "$group:${project.name}"
                description = project.description
                url = project.url

                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }

                developers {
                    developer {
                        name = "pvtyuan"
                        email = "pvtyuan@foxmail.com"
                    }
                }

                scm {
                    connection = "scm:git:${project.vcsUrl}"
                    developerConnection = "scm:git:${project.vcsUrl}"
                    url = project.url
                }
            }
        }
    }
    repositories {
        maven {
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username mavenCentralUsername
                password mavenCentralPassword
            }
        }
    }
}

// include pom in jar
jar {
    into("META-INF/maven/$project.group/$project.name") {
        from generatePomFileForMavenJavaPublication
        rename ".*", "pom.xml"
    }
}

release {
    failOnUnversionedFiles = false
    pushReleaseVersionBranch = "master"
}

//spotbugsMain {
//    reports {
//        html {
//            enabled = true
//            destination = file("$buildDir/reports/spotbugs/main/spotbugs.html")
//            stylesheet = 'fancy-hist.xsl'
//        }
//    }
//}

signing {
    sign publishing.publications
}

afterReleaseBuild.dependsOn publish
